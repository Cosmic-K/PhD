;+
;NAME: TEST_RESIDUALS
;
;PURPOSE: 
;   Quantify the goodness-of-fit of a given data model by appliying two statistical
;   tests on the residuals. Tests for both a normal distribution, using the
;   Kolmogorov–Smirnov (KS) test, and autocovarience, using the Ljung–Box test.
;
;INPUTS:
;   residuals - values obtained by subtracting a model results from the data
;
;OPTIONAL INPUTS:
;   signif_level - significance level to use for determining the chi squared
;                  cut-off limit for the Ljung-Box test. Default is 0.95
;   fit_params - number of parameters fit by the model (or combination of models).
;                Used to apply a correction to the degrees of freedom in determining
;                the chi squared cut-off value. Default is 0 (no model)
;   max_lag - maximum autocorrelation lag to include in Ljung-Box test.
;             Default is num_residuals/2 (this is also the maximum value allowed)
;
;
;OUTPUTS:
;   test_stats - structure containing the test statistics as well as their
;                threshold values and probabilities
;
; 
;HISTORY: Name---------Date---------Description
;         M Weberg  July, 2016 - Initial coding
;-

FUNCTION TEST_RESIDUALS, residuals, signif_level=signif_level, num_fit_params=num_fit_params, max_lag=max_lag, debug_hist=debug_hist 

sigma = stddev(residuals)

N = n_elements(residuals)
half_n = floor(N/2.0)

;Set defualt significance level
IF n_elements(signif_level) EQ 0 THEN signif_level = 0.95

;Set defualt number of parameters that have been fit by the data model being tested
IF n_elements(num_fit_params) EQ 0 THEN num_fit_params = 0

;Set default number of lags to test in Ljung-Box test
;NOTE: will cap the maximum allowable lag at half the number of input data points
IF NOT KEYWORD_SET(max_lag) THEN BEGIN 
    max_lag = half_n 
ENDIF ELSE BEGIN
    IF half_n LT max_lag THEN max_lag = half_n
ENDELSE

;Performing the Kolmogorov–Smirnov two sample test to determine normality
standard_residuals = residuals/sigma
resid_hist = HISTOGRAM(standard_residuals, LOCATIONS=resid_bins, min=-6, max=6, nbins=12)
resid_hist = 1.0*resid_hist/(n_elements(residuals))

gauss_x_vals = 5*(findgen(1001) - 500)/500
gauss_y_vals = 1/(sqrt(2*!pi))*EXP(-gauss_x_vals^2/2.0)

KSTWO, resid_hist, gauss_y_vals, KS_stat, KS_prob

;Performing the Ljung-Box test to detect autocorrelation
chisqr_cutoff = chisqr_CVF(1.0-signif_level, max_lag-num_fit_params)
lags = indgen(max_lag) + 1
AR = fltarr(max_lag)
mu = mean(residuals)
mean_diff = residuals - mu
FOR k = 0L, max_lag-1 DO BEGIN
    AR[k] = total(mean_diff[0:-lags[k]-1]*mean_diff[lags[k]:*])
ENDFOR

AR = AR/total(mean_diff^2)

Q = N*(N+2)*total(AR^2/(N-lags))

;Initialize output array
test_stats = {signif_level:signif_level, $
              KS_stat:KS_stat, $
              KS_prob:KS_prob, $
              Q:Q, $
              Q_chi_sqrd:chisqr_cutoff}

debug_hist = {residuals:residuals, $
              norm_resid:standard_residuals, $
              hist_vals:resid_hist, $
              hist_bins:resid_bins}                

RETURN, test_stats
END


